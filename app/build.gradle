plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def locales = file('src/main/res').listFiles(new FilenameFilter() {
    @Override
    boolean accept(File dir, String name) {
        return name.matches('values-.*') && new File(dir, name).list().contains('strings.xml')
    }
}).collect { it.name.substring(7) }

print("Locales: " + locales.join(', '))

android {
    compileSdk 34

    defaultConfig {
        applicationId "de.dlyt.yanndroid.notinotes"
        minSdk 26
        targetSdk 34
        versionCode 13
        versionName "1.7.2"

        resConfigs locales
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'de.dlyt.yanndroid.notinotes'
}

configurations.all {
    exclude group: 'androidx.core', module: 'core'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'io.github.oneuiproject.sesl:appcompat:1.4.0'
    implementation 'io.github.oneuiproject.sesl:picker-color:1.1.0'
    implementation 'com.google.code.gson:gson:2.10.1'
}